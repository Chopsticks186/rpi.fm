#!/usr/bin/perl
# Copyright: SZABO Gergely <szg@subogero.com>, WTFPL 2.0
use LWP::Simple;
use HTML::Parse;
use Term::ANSIColor qw(:constants);
use integer;
sub getstation; sub getgenre; sub loadgenres; sub player; sub status; sub cols;
sub help {
    print <<EOF;
Available commands:
q	Quit				f	Featured stations list
d	Demonize player	and quit	g	choose Genre
?	help				l	List actual stations
h	play on HDMI output		p	Play/Pause
j	play on stereo Jack output	s	Show what's playing
EOF
}

if ($ARGV[0] =~ /-h/) {
    help;
    exit 0;
}

print <<EOF;
Welcome to rpi.fm - Type ? for help
===================================
EOF

our @stations;
our %genres;
our $genre;
our $title;
our $station_now;
our $output;
our $nowfile = "$ENV{HOME}/.rpi.fm.now";
our $pidfile = '/run/lock/rpi.fm.pid';

player;
print BRIGHT_RED, "rpi.fm> ", RESET;
while (<>) {
    (my $cmd = $_) =~ s/\r|\n//g;
    if      ($cmd eq 'q') {
        player $cmd;
        exit 0;
    } elsif ($cmd eq 'd') {
        exit 0;
    } elsif ($cmd eq '?') {
        help;
    } elsif ($cmd eq 'f') {
        $genre = '';
        getstation;
    } elsif ($cmd eq 'l') {
        getstation;
    } elsif ($cmd eq 'g') {
        getgenre;
    } elsif ($cmd =~ /[hjp]/) {
        player $cmd;
    } elsif ($cmd eq 's') {
        status;
    }
    print BRIGHT_RED, "rpi.fm> ", RESET;
}

sub getstation {
    my $page = get "http://www.internet-radio.com$genre";
    loadgenres $page unless %genres;
    $page =~ /<h2 style=.+>\s*(.+)<\/h2>/;
    $title = $1;
    my $doc = parse_html $page;
    my $i = 0;
    print "$title\n";
    my $cols = cols;
    my $ws = ($cols - 4) / 4;
    my $wt = ($cols - 4) * 3 / 4;
    foreach (@{$doc->extract_links()}) {
        next unless $_->[0] =~ /mount=(.+pls)&title=(.+)&website=http:\/\/(.+)$/;
        my ($listen, $title, $station) = ($1, $2, $3);
        $stations[$i] = $listen;
        printf "%2d %-${ws}.${ws}s %-${wt}.${wt}s\n", $i, $station, $title;
        $i++;
    }
    my $n;
    while ($n !~ /\d+/ || $n < 0 || $n >= $i) {
        return if $i == 0;
        print BRIGHT_RED, "rpi.fm Choose station> ", RESET;
        $n = <>;
    }
    $station_now = $stations[$n];
}

sub getgenre {
    loadgenres unless %genres;
    my $i = 0;
    my $width = cols() / 4 - 5;
    foreach (sort keys %genres) {
        printf "%3d %-${width}.${width}s ", $i++, $_;
        print "\n" if $i % 4 == 0;
    }
    print "\n" if $i % 4 != 0;
    my $n;
    while ($n !~ /\d+/ || $n < 0 || $n >= $i) {
        print BRIGHT_RED, "rpi.fm Choose genre> ", RESET;
        $n = <>;
    }
    my $key = (sort keys %genres)[$n];
    $genre =  $genres{$key};
    getstation;
}

sub loadgenres {
    my @lines = split /\n/, $_[0] || get "http://www.internet-radio.com";
    foreach (@lines) {
        $genres{$2} = $1 if /<option value="(.+)">(.+)<\/option>/;
    }
}

sub player {
    # Load station and output from nowfile
    if (!$station_now && open NOW, $nowfile) {
        ($station_now = <NOW>) =~ s/\n//;
        ($output      = <NOW>) =~ s/\n//;
        close NOW;
    }
    # Load player PID from pidfile
    our $player_pid;
    if (!$player_pid && -f $pidfile) {
        open PID, $pidfile or die $!;
        $player_pid = <PID>;
        close PID;
        return;
    }
    # Kill actual player upon other command
    if ($player_pid) {
        kill TERM => -$player_pid;
        $player_pid = undef;
        unlink $nowfile if $_[0] ne 'q';
        unlink $pidfile;
        return if $_[0] eq 'p';
    }
    return if $_[0] eq 'q' || !$station_now;
    # Save station and output into nowfile
    $output = $_[0] eq 'j' ? '-olocal' : $_[0] eq 'h' ? '-ohdmi' : $output;
    if (open NOW, ">$nowfile") {
        print NOW "$station_now\n";
        print NOW "$output\n";
        close NOW;
    }
    # Parent saves child PID, child execs omxplayer
    $player_pid = fork;
    if ($player_pid) {
        open PID, ">$pidfile" or die $!;
        print PID $player_pid;
        close PID;
    } else {
        my $file = (split /=|\n/, get($station_now))[-1];
        setpgrp;
        open STDOUT, ">/dev/null";
        close STDIN;
        exec '/usr/bin/omxplayer', $output, $file;
    }
}

sub status {
    (my $out = $output) =~ s/-o//;
    if (!$station_now && ! -f $pidfile) {
        print "Stopped\n";
    } elsif (!$station_now && -f $pidfile) {
        print "Another user is listening to rpi.fm\n";
    } elsif ($station_now && ! -f $pidfile) {
        print "Paused $station_now on $out\n";
    } elsif ($station_now && -f $pidfile) {
        print "Playing $station_now on $out\n";
    }
}

sub cols {
    my ($rows, $cols) = split /[ \n]/, `/bin/stty size`;
    return $cols;
}
